plugins {
	id 'java'
    id 'application' 
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id 'org.beryx.jlink' version '2.26.0'
	id 'eclipse'
}

group 'com.github.coughingDragon'
version '1.0'

repositories {
    mavenCentral() 
}

ext {
	junitVersion = '5.10.2'
}

dependencies {
	testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
	
	runtimeOnly 'org.apache.logging.log4j:log4j-api:2.23.1'
	implementation 'org.apache.poi:poi-ooxml:5.2.5'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

application {
	mainModule = 'com.github.coughingDragon.kritzel'
    mainClass = 'com.github.coughingDragon.kritzel.Main'
}

javafx {
    version = "21.0.2"
    modules = [ 'javafx.controls', 'javafx.graphics' ]
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform() 
}

jlink {
	imageZip.set(file("$buildDir/image-zip/kritzel.zip"))
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	
    launcher {
        name = 'kritzel'
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
    }
    
	targetPlatform('win') {
		jdkHome = jdkDownload("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.10%2B7/OpenJDK17U-jdk_x64_windows_hotspot_17.0.10_7.zip")
		addExtraModulePath("$buildDir/jdks/win/jdk-17.0.10+7/jmods")
    }
 
    jpackage {
		skipInstaller = false
		if (org.gradle.internal.os.OperatingSystem.current().windows) {
			jpackageHome = "$buildDir/jdks/win/jdk-17.0.10+7"
        	targetPlatformName = "win"
        	installerType = "msi"
        	icon = "src/main/resources/icons/favicon.ico"
        	installerOptions = ["--win-menu", "--win-shortcut"]
    	}
	}   
}   